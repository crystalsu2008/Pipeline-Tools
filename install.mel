global proc toolsSourcePathPointer(){}

global proc string toolsSourcePath()
{
  string $whatIs=`whatIs toolsSourcePathPointer`;
  string $fullPath=`substring $whatIs 25 999`;
  $path=toNativePath(dirname($fullPath));
  return $path;
}

string $prepareSetup = "import sys\n"
  + "Dir = '" + toolsSourcePath() + "'\n"
  + "if Dir not in sys.path:\n"
  + "    sys.path.append(Dir)\n"
  + "import mayaPipelineSetup;reload(mayaPipelineSetup)\n";
python( $prepareSetup );

// Install Pipeline Tools and get the Tools name.
string $pipelineRepositoryName = python("mayaPipelineSetup.install(source=Dir, installToVersionDir=True)");


if (`window -ex "PipelineConfirmWindow"`)
  deleteUI -window "PipelineConfirmWindow";

string $cmd = "python(\"from "+$pipelineRepositoryName+".pipelineStartup import *\");\n";
string $pipelineConfirmWindow;
if (int(`about -v`) < 2016)
  $pipelineConfirmWindow = `window -t "Install Pipeline Tools" -w 300 -h 90 -s 0 -tb 0 "PipelineConfirmWindow"`;
else
  $pipelineConfirmWindow = `window -t "Install Pipeline Tools" -w 300 -h 90 -s 0 -mnb 0 -mxb 0 -cc $cmd "PipelineConfirmWindow"`;
string $form = `formLayout -numberOfDivisions 100`;
$cmd += "deleteUI -window " + $pipelineConfirmWindow + ";";
string $confirmButton = `button -label "installation is completed!" -command $cmd`;
formLayout -edit
    -attachForm     $confirmButton     "top"    30
    -attachPosition $confirmButton     "left"   0 20
    -attachForm     $confirmButton     "bottom" 30
    -attachPosition $confirmButton     "right"  0 80
$form;
showWindow $pipelineConfirmWindow;
