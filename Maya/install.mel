int $PiplineTollsAutoInstall = 0;
int $InstallToVersionDir = 1;
//----------------------------------------------------------------------------//
global proc toolsSourcePathPointer(){}

global proc string toolsSourcePath()
{
  string $whatIs=`whatIs toolsSourcePathPointer`;
  string $fullPath=`substring $whatIs 25 999`;
  $path=toNativePath(dirname($fullPath));
  return $path;
}

proc string getPiplineToolsInstallDestination(int $InstallToVersionDir)
{
  string $dest;
  if ($InstallToVersionDir)
  {
    $dest = dirname(`internalVar -upd`);
    $dest = toNativePath($dest+"/scripts");
  }
  else
  {
    $dest = `internalVar -uad`;
    $dest = toNativePath($dest+"scripts");
  }
  return $dest;
}

global proc string PiplineToolsRefreshPyCmds(string $module, string $destination)
{
  string $cmds = "import sys\n"
        + "Dir = '" + $destination + "'\n"
        + "if Dir not in sys.path:\n"
        + "    sys.path.append(Dir)\n"
        + "import "+$module+"\n"
        + "reload("+$module+")\n"
        + "from "+$module+".pipelineStartup import *\n"
        + "reload ("+$module+".pipelineStartup)";
  return $cmds;
}
//----------------------------------------------------------------------------//
string $source = toolsSourcePath();
string $destination = getPiplineToolsInstallDestination($InstallToVersionDir);

string $prepareInstall = "import sys\n"
  + "Dir = '" + encodeString($source) + "'\n"
  + "if Dir not in sys.path:\n"
  + "    sys.path.append(Dir)\n"
  + "import mayaPipelineSetup;reload(mayaPipelineSetup)\n";
python( $prepareInstall );

if ( $PiplineTollsAutoInstall )
{
  // Install Pipeline Tools and get the Tools name.
  string $module = `python("mayaPipelineSetup.install(source=Dir, destination='"+encodeString($destination)+"')")`;
  // Refresh the Tools.
  string $refreshCmds = PiplineToolsRefreshPyCmds($module, $destination);
  // !!! I don't why, when the tools frist refresh in the Maya 2014, there is a warning:
  // UnicodeWarning: Unicode equal comparison failed to convert both arguments to Unicode - interpreting them as being unequal, at line 4, in  "D:\Program Files\Autodesk\Maya2014\bin\maya.exe"
  // So I must do double refresh, then it's OK!!!
  python($refreshCmds);
  python($refreshCmds);
}
else
{
  //template.define( pm.button, w=200, h=30, al='left' )
  if (`window -ex "PipelineToolsInstallWin"`)
      deleteUI -window "PipelineToolsInstallWin";

    $win = `window -mb 0 -mbv 0 -t "Pipeline Tools Install" "PipelineToolsInstallWin"`;
      $form = `formLayout -nd 100`;
        $frame = `frameLayout -bv 1 -lv 0 -mh 5 -mw 1`;
          rowLayout -adj 1;
            columnLayout -rowSpacing 5 -adj 1;
              //$sourField = textFieldButtonGrp -l "Source Path:" -fi `toolsSourcePath()` -bl="   Browse   ", -cw3 65 265 70 -w 400;
              $destField = `textFieldButtonGrp -l "Install Path:" -fi $destination -bl "   Browse   "
                          -cw3 65 265 70 -adj 2 -cal 1 "right" -cat 1 "both" 0 -cat 2 "both" 0 -cat 3 "both" 0`;
              string $browseCmd = "string $caption = \"Pick a Directory to install Repository\";\n"
                                + "string $destination = `textFieldButtonGrp -q -fi \""+$destField+"\"`;\n"
                                + "textFieldButtonGrp -e -fi (`fileDialog2 -cap $caption -fm 3 -dir $destination`) "+$destField+";";
              textFieldButtonGrp -e -bc $browseCmd $destField;
              setParent ..;
            setParent ..;
          setParent ..;
          $row = `rowLayout -nc 3 -cw3 200 100 100 -cal 1 "right" -cat 1 "both" 0 -adj 1`;
            formLayout; setParent ..;
            $insButtnCmd = "string $source = \""+encodeString($source)+"\";\n"
                         + "string $destination = `textFieldButtonGrp -q -fi "+$destField+"`;\n"
                         + "$destination = encodeString(`toNativePath($destination)`);\n"
                         + "string $module = `python(\"mayaPipelineSetup.install(source='\"+$source+\"', destination='\"+$destination+\"')\")`;\n"
                         + "string $refreshCmds = PiplineToolsRefreshPyCmds($module, $destination);\n"
                         + "python($refreshCmds);python($refreshCmds);\n";
            $closeWinCmd = "deleteUI -window "+$win+";";
            button -l "Install" -w 100 -h 30 -al "left" -c ($insButtnCmd+$closeWinCmd);
            button -l "Cancel" -w 100 -h 30 -al "left" -c $closeWinCmd;

  formLayout -e
    -attachForm     $frame "top"    5
    -attachForm     $frame "left"   5
    -attachForm     $frame "right"  5
    -attachControl  $frame "bottom" 5 $row

    -attachForm     $row   "bottom" 5
    -attachForm     $row   "left"   5
    -attachForm     $row   "right"  5
  $form;

  showWindow $win;
}
